module List exposing
    ( List((::)) -- don't export Nil, as it can be refered to as `[]`
    , map, flatMap, head, tail, length, foldl, foldr, fold, reverse, append
    , repeat, range, contains
    )

import Basics exposing (..)
import Option exposing (Option(Some, None))
import Computation exposing (Computation, Failable)


type List a = Nil | (::) a (List a)

infixr 5 ::

map : forall a b . (a -> b) -> List a -> List b
map f list = case list of
    [] -> []
    x :: xs -> f x :: map f xs

flatMap : forall a b . (a -> List b) -> List a -> List b
flatMap f list = fold (map f list)

head : forall a . List a -> Option a
head list = case list of
    [] -> None
    x :: _ -> Some x

tail : forall a . List a -> Option (List a)
tail list = case list of
    [] -> None
    _ :: xs -> Some xs

length : forall a . List a -> Int
length list = case list of
    [] -> 0
    _ :: xs -> 1 + length xs

foldl : forall a b . a -> (a -> b -> a) -> List b -> a
foldl init f list = case list of
    [] -> init
    x :: xs -> foldl (f init x) f xs

foldr : forall a b . a -> (b -> a -> a) -> List b -> a
foldr init f list = case list of
    [] -> init
    x :: xs -> f x (foldr init f xs)

fold : forall a : Appendable, Default; . List a -> a
fold = foldl default (++)

reverse : forall a . List a -> List a
reverse list = foldl [] (flip (::)) list

append : forall a . List a -> List a -> List a
append a b = case a of
    [] -> b
    x :: xs -> x :: append xs b

repeat : forall a . Int -> a -> List a
repeat amount value =
    if amount <= 0 then
        []
    else
        value :: repeat (amount - 1) value

range : Int -> Int -> List Int
range from to =
    if from > to then
        []
    else
        from :: range (from + 1) to

contains : forall a : Eq; . a -> List a -> Bool
contains value list = case list of
    [] -> false
    x :: xs -> x == value || contains value xs

impl forall a . List a : Appendable where
    (++) = append

impl List : Computation where
    succeed x = [x]
    andThen = flatMap

impl List : Failable where
    fail = []

impl forall a . List a : Default where
    default = []

impl List : Mappable where
    map = map

impl forall a : Eq; . List a : Eq where
    a == b = case (a, b) of
        ([], []) -> true
        (x :: xs, y :: ys) -> x == y && xs == ys
        _ -> false

impl forall a : Ord; . List a : Ord where
    compare a b = case (a, b) of
        ([], []) -> EQ
        (_ :: _, []) -> GT
        ([], _ :: _) -> LT
        (x :: xs, y :: ys) ->
            case compare x y of
                EQ -> compare xs ys
                other -> other

impl forall a : ToString; . List a : ToString where
    toString list = 
        let
            items acc list = case list of
                [] -> acc
                x :: xs -> items (acc ++ ", " ++ toString x) xs
        in
            case list of
                [] -> "[]"
                [x] -> "[" ++ toString x ++ "]"
                x :: xs -> "[" ++ toString x ++ items "" xs ++ "]"
